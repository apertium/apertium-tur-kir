Multichar_Symbols

%<n%>   ! Noun
%<np%>  ! Proper noun
%<adv%>  ! Adverb
%<post%> ! Postposition
%<nom%>  ! Nominative
%<acc%>  ! Accusative
%<dat%>  ! Dative
%<gen%>  ! Genitive
%<loc%>  ! Locative
%<abl%>  ! Ablative
%<pl%>  ! Plural
%<ant%>  ! Anthroponym (name of person)
%<m%>   ! Masculine
%<px1sg%> ! Possessive, first person singular
%<px2sg%> !
%<px3sg%> !
%<px1pl%> !
%<px2pl%> !
%<px3pl%> !

%{A%}   ! Realised as 'а', 'е', 'о', 'ө' 
%{I%}   ! Realised as 'ы', 'и', 'у', 'ү'
%{G%}   ! Realised as 'г', 'к', 'ң', 'н'
%{D%}   ! Realised as 'д', 'т', 'н'
%{M%}   ! Realised as 'м', 'б', 'п'
%{N%}   ! Realised as 'н', 'д', 'т'
%{L%}   ! Realised as 'л', 'д', 'т'


LEXICON Root

NounStems ;
PropernounStems ;
AdvStems ;
Postpositions ;

LEXICON Ncases
%<nom%>:%> # ;      ! -     -      -
%<gen%>:%>%{N%}%{I%}н # ; ! нын, -нин, -дын, -дин, -тын, -тин, -нун, -нүн, -дун, -дүн, -тун, -түн
%<dat%>:%>%{G%}%{A%} # ; ! -га, -ка, -ге, -ке, -го, -ко, -гө, -кө
%<acc%>:%>%{N%}%{I%} # ; ! -ны, -ни, -ды, -ди, -ты, -ти, -ну, -нү, -ду, -дү, -ту, -тү
%<loc%>:%>%{D%}%{A%} # ; ! -да, -де, -та, -те, -до, -дө, -то, -тө
%<abl%>:%>%{D%}%{A%}н # ; ! -дан, -ден, -тан, -тен, -дон, -дөн, -тон, -төн

LEXICON Posses
Ncases ;
%<px1sg%>:%>%{I%}м Ncases ;
%<px2sg%>:%>%{I%}ң Ncases ; 
%<px3sg%>:%>%{I%} Ncases ;
%<px1pl%>:%>б%{I%}з Ncases ; 
%<px2pl%>:%>ң%{I%}з Ncases ; 
%<px3pl%>:%>%{D%}%{A%}%{I%} Ncases ;

LEXICON Adv

%<adv%>: # ;

LEXICON Post 

%<post%>: # ;

LEXICON Ninfl

%<n%>: Posses ;
%<n%>%<pl%>:%>%{L%}%{A%}р Posses ;


LEXICON NpAntMInfl1

%<np%>%<ant%>%<m%>: # ;

LEXICON NounStems

аба Ninfl ; ! "air"
ай Ninfl ; ! "month"
апа Ninfl ; ! "mother"
аскер Ninfl ; ! "soldier" 
баш Ninfl ; ! "head"
жан Ninfl ; ! "jan"
кеме Ninfl ; ! "boat"
китеп Ninfl ; ! "book"
кол Ninfl ; ! "hand"
көз Ninfl ; ! "eye"
мектеп Ninfl ; ! "school"
сыра Ninfl ; ! "beer"
таң Ninfl ; ! "dawn"
тор Ninfl ; ! "net"
туз Ninfl ; ! "salt"
чака Ninfl ; ! "bucket"
челек Ninfl ; ! "bucket"



LEXICON PropernounStems

Ахмет NpAntMInfl1 ; ! "Axmet"

LEXICON AdvStems

эртең Adv ; ! "tomorrow"	

LEXICON Postpositions

менен Post ; ! ""
