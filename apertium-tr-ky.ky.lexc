!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!   M O R P H O L O G I C A L · T R A N S D U C E R · F O R · K Y R G Y Z   !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

Multichar_Symbols

%<n%>      ! Noun
%<np%>     ! Proper noun
%<adj%>    ! Adjective
%<det%>    ! Determiner
%<adv%>    ! Adverb
%<num%>    ! Numeral
%<v%>      ! Verb
%<prn%>    ! Pronoun
%<post%>   ! Postposition
%<part%>   ! Particle
%<cnjcoo%> ! Coordinating conjunction
%<cnjadv%> ! Adverbial conjunction
%<cnjsub%> ! Subordinating conjunction

%<nom%>    ! Nominative
%<acc%>    ! Accusative
%<dat%>    ! Dative
%<gen%>    ! Genitive
%<loc%>    ! Locative
%<abl%>    ! Ablative

%<rloc%>   ! Relativised locative -DAGI

%<m%>      ! Masculine
%<f%>      ! Feminine

%<pers%>   ! Personal 
%<dem%>    ! Demonstrative

%<sg%>     ! Singular
%<pl%>     ! Plural

%<p1%>     ! First person
%<p2%>     ! Second person
%<p3%>     ! Third person

%<px1sg%>  ! Possessive, first person singular
%<px2sg%>  ! Possessive, second person singular
%<px3sg%>  ! Possessive, third person singular
%<px1pl%>  ! Possessive, first person plural
%<px2pl%>  ! Possessive, second person plural
%<px3pl%>  ! Possessive, third person plural
%<px%>     ! General possessive -ныкы

%<ant%>    ! Anthroponym (name of person)
%<top%>    ! Toponym

%<frm%>    ! Formal

%<dem%>    ! Demonstrative
%<neg%>    ! Negative
%<exi%>    ! Existential
%<qst%>    ! Question

%<cnvi%>   ! Converb imperfect
%<cnvp%>   ! Converb perfect
%<pres%>   ! Present tense
%<past%>   ! Past tense
%<pp%>     ! Past participle
%<plist%>  ! Listing participle
%<inf%>    ! Verbal noun in -үү
%<inf2%>   ! Verbal noun in -иш

%<sent%>   ! Sentence marker
%<cm%>     ! Comma


%{AJ%}     ! Realised as 'а', 'й' 
%{A%}      ! Realised as 'а', 'е', 'о', 'ө' 
%{I%}      ! Realised as 'ы', 'и', 'у', 'ү'
%{G%}      ! Realised as 'г', 'к', 'ң', 'н'
%{D%}      ! Realised as 'д', 'т', 'н'
%{M%}      ! Realised as 'м', 'б', 'п'
%{N%}      ! Realised as 'н', 'д', 'т'
%{L%}      ! Realised as 'л', 'д', 'т'
%{B%}      ! Realised as 'б', 'в' **CHECK**


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                  C O N T I N U A T I O N      L E X I C A               !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

!NumeralList ;
Particles ;
Postpositions ;
Conjunctions ;
Determiners ;
Pronouns ;
Numerals ;
NounStems ;
PropernounStems ;
AdjecStems ;
AdvStems ;
VerbStems ;
Punctuation ;

LEXICON CASES
%<nom%>:%> # ;             ! -     -      -
%<gen%>:%>%{N%}%{I%}н # ;  ! -нын, -нин, -дын, -дин, -тын, -тин, -нун, -нүн, -дун, -дүн, -тун, -түн
%<dat%>:%>%{G%}%{A%} # ;   ! -га, -ка, -ге, -ке, -го, -ко, -гө, -кө
%<acc%>:%>%{N%}%{I%} # ;   ! -ны, -ни, -ды, -ди, -ты, -ти, -ну, -нү, -ду, -дү, -ту, -тү
%<loc%>:%>%{D%}%{A%} # ;   ! -да, -де, -та, -те, -до, -дө, -то, -тө
%<abl%>:%>%{D%}%{A%}н # ;  ! -дан, -ден, -тан, -тен, -дон, -дөн, -тон, -төн
%<rloc%>:%>%{D%}%{A%}г%{I%} # ;

LEXICON GEN-POS
CASES ;
%<px%>:%>%{N%}%{I%}к%{I%} CASES ; 

LEXICON POSSESSION
GEN-POS ;
%<px1sg%>:%>%{I%}м GEN-POS ;
%<px2sg%>:%>%{I%}ң GEN-POS ; 
%<px2sg%>%<frm%>:%>%{I%}ң%{I%}з GEN-POS ; 
%<px3sg%>:%>с%{I%} GEN-POS ;
%<px1pl%>:%>%{I%}б%{I%}з GEN-POS ; 
%<px2pl%>:%>%{I%}ң%{A%}р GEN-POS ; 
%<px2pl%>%<frm%>:%>%{I%}ң%{I%}зд%{A%}р GEN-POS;
%<px3pl%>:%>с%{I%} GEN-POS ;

LEXICON ADJ

%<adj%>: # ;

LEXICON ADV

%<adv%>: # ;

LEXICON POST

%<post%>: # ;

LEXICON CA

%<cnjadv%>: # ;

LEXICON CC

%<cnjcoo%>: # ;

LEXICON PART 

%<part%>: # ;

LEXICON DET-DEM

%<det%>%<dem%>: # ;

LEXICON NumeralList

%0 NumeralLoop ;
1 NumeralLoop ;
2 NumeralLoop ;
3 NumeralLoop ;
4 NumeralLoop ;
5 NumeralLoop ;
6 NumeralLoop ;
7 NumeralLoop ;
8 NumeralLoop ;
9 NumeralLoop ;

LEXICON NumeralLoop
NumeralList ;
%. NumeralList  ;
%, NumeralList  ;
%%%<num%>:%% CASES ;
Num ;

LEXICON NUM

%<num%>: CASES ;

LEXICON PRON-DEM-BUL

%<prn%>%<dem%>%<sg%>%<nom%>:бул # ;
%<prn%>%<dem%>%<sg%>%<gen%>:мунун # ;
%<prn%>%<dem%>%<sg%>%<acc%>:муну # ;
%<prn%>%<dem%>%<sg%>%<dat%>:буга # ;
%<prn%>%<dem%>%<sg%>%<loc%>:мында # ;
%<prn%>%<dem%>%<sg%>%<abl%>:мындан # ;

%<prn%>%<dem%>%<pl%>:бул%>л%{A%}р CASES ;

LEXICON PRON-DEM-OSOL

%<prn%>%<dem%>%<sg%>%<nom%>:л # ;
%<prn%>%<dem%>%<sg%>%<gen%>:нун # ;
%<prn%>%<dem%>%<sg%>%<acc%>:ну # ;
%<prn%>%<dem%>%<sg%>%<dat%>:го # ;
%<prn%>%<dem%>%<sg%>%<loc%>:ндо # ;
%<prn%>%<dem%>%<sg%>%<abl%>:ндон # ;

%<prn%>%<dem%>%<pl%>:%>%{L%}%{A%}р CASES ;

LEXICON PRON-DEM-AL

%<prn%>%<dem%>%<sg%>%<nom%>:л # ;
%<prn%>%<dem%>%<sg%>%<gen%>:нын # ;
%<prn%>%<dem%>%<sg%>%<acc%>:ны # ;
%<prn%>%<dem%>%<sg%>%<dat%>:га # ;
%<prn%>%<dem%>%<sg%>%<loc%>:нда # ;
%<prn%>%<dem%>%<sg%>%<abl%>:ндан # ;

%<prn%>%<dem%>%<pl%>:%>%{L%}%{A%}р CASES ;

LEXICON PRON-DEM-TIGIL

%<prn%>%<dem%>%<sg%>%<nom%>:л # ;
%<prn%>%<dem%>%<sg%>%<gen%>:нин # ;
%<prn%>%<dem%>%<sg%>%<acc%>:ни # ;
%<prn%>%<dem%>%<sg%>%<dat%>:ге # ;
%<prn%>%<dem%>%<sg%>%<loc%>:нде # ;
%<prn%>%<dem%>%<sg%>%<abl%>:нден # ;

%<prn%>%<dem%>%<pl%>:%>%{L%}%{A%}р CASES ;

LEXICON PRON-PERS-SEN

%<nom%>:ен # ;
%<gen%>:енин # ;
%<acc%>:ени # ;
%<dat%>:ага # ;
%<loc%>:енде # ;
%<abl%>:енден # ;

LEXICON PRON-PERS-SIZ

%<nom%>:из # ;
%<gen%>:изди # ;
%<acc%>:издин # ;
%<dat%>:изге # ;
%<loc%>:изде # ;
%<abl%>:изден # ;

LEXICON PRON-PERS-AL

%<nom%>:л # ;
%<gen%>:ны # ;
%<acc%>:нын # ;
%<dat%>:га # ;
%<loc%>:нда # ;
%<abl%>:ндан # ;

LEXICON PRON-PERS-SILER

%<nom%>: # ;
%<gen%>:%>д%{I%} # ;
%<acc%>:%>д%{I%}н # ;
%<dat%>:%>г%{A%} # ;
%<loc%>:%>д%{A%} # ;
%<abl%>:%>д%{A%}н # ;

LEXICON N-INFL

%<n%>: POSSESSION ;
%<n%>%<pl%>:%>%{L%}%{A%}р POSSESSION ;
%<n%>%<pl%>:%>%{D%}%{A%}р POSSESSION ;

LEXICON NP-ANT-M

%<np%>%<ant%>%<m%>: CASES  ;

LEXICON NP-ANT-F

%<np%>%<ant%>%<f%>: CASES ;


LEXICON NP-TOP

%<np%>%<top%>: CASES ;

LEXICON V-CLIT 

%+би%<qst%>:%>%{B%}%{I%} # ;
# ;

LEXICON V-PERS-PRON

%<p1%>%<sg%>:%>м%{I%}н V-CLIT ; 
%<p2%>%<sg%>:%>с%{I%}ң V-CLIT ; 
%<p3%>%<sg%>: V-CLIT ; 
%<p1%>%<pl%>:%>б%{I%}з V-CLIT ; 
!%<p2%>%<pl%>:%> V-CLIT ;  
!%<p3%>%<pl%>:%>ш V-CLIT ; 


LEXICON V-PERS-POSS

%<p1%>%<sg%>:%>м V-CLIT ; 
%<p2%>%<sg%>:%>ң V-CLIT ; 
%<p3%>%<sg%>: V-CLIT ; 
%<p1%>%<pl%>:%>к V-CLIT ; 
!%<p2%>%<pl%>:%> V-CLIT ;  
%<p3%>%<pl%>:%>ш V-CLIT ; 

LEXICON V-INFL-COMMON

%<v%>: # ;
%<v%>%<cnvi%>:%>%{AJ%} # ;
%<v%>%<cnvp%>:%>%{I%}п # ;
%<v%>%<pp%>:%>%{G%}%{A%}н CASES ;
%<v%>%<plist%>:%>м%{A%}й # ;
%<v%>%<neg%>%<pres%>:%>%{B%}%{A%} V-PERS-PRON ;
%<v%>%<pres%>:%>%{A%} V-PERS-POSS ;
%<v%>%<past%>:%>%{D%}%{I%} V-PERS-POSS ;

LEXICON V-INFL

V-INFL-COMMON ;
%<v%>%<inf%>:%>үү # ;
%<v%>%<inf2%>:%>%{I%}ш # ;

LEXICON V-INFL-SHORT

V-INFL-COMMON ;
%<v%>%<inf2%>:%>%{I%}ш # ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                  L E X I C O N    E N T R I E S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Particles 

да PART ; ! "also, too"
го PART ; ! "also, too"
гана PART ; ! "only"
эле PART ; ! "only"
мына PART ; ! "here is"

LEXICON Conjunctions 

жана CC ; ! "and"
да CC ; ! "and"
же CC ; ! "or"
менен CC ; ! "and"
не CC ; ! "neither... nor..."
ал CA ; ! "but"
бирок CA ; ! "but"

LEXICON Postpositions

менен POST ; ! "with"
дейре POST ; ! "up to, until"
чейин POST ; ! "up to, until"
кийин POST ; ! "with"
керек POST ; ! ""
сайын POST ; ! "every"
үчүн POST ; ! "for"
жакка POST ; ! "toward"
чакты POST ; ! "about, around"

LEXICON Determiners

бул DET-DEM ; ! "this"
ошол DET-DEM ; ! "that"
ал DET-DEM ; ! "that"
тигил DET-DEM ; ! "that (yonder)"

LEXICON Pronouns

мен%<prn%>%<pers%>%<p1%>%<sg%>:м PRON-PERS-SEN ; 
сен%<prn%>%<pers%>%<p2%>%<sg%>:с PRON-PERS-SEN ; 
сиз%<prn%>%<pers%>%<p2%>%<frm%>%<sg%>:с PRON-PERS-SIZ ; 
ал%<prn%>%<pers%>%<p3%>%<sg%>:а PRON-PERS-AL ; 
биз%<prn%>%<pers%>%<p1%>%<pl%>:б PRON-PERS-SIZ ; 
силер%<prn%>%<pers%>%<p2%>%<pl%>:силер PRON-PERS-SILER ; 
сиздер%<prn%>%<pers%>%<p2%>%<frm%>%<pl%>:сиздер PRON-PERS-SILER ; 
алар%<prn%>%<pers%>%<p3%>%<pl%>:алар PRON-PERS-SILER ; 

бул: PRON-DEM-BUL ; ! "this"
ошол:ошо PRON-DEM-OSOL ; ! "that"
ал:а PRON-DEM-AL ; ! "that"
тигил:тиги PRON-DEM-TIGIL ; ! "that (yonder)

LEXICON Numerals 

бир NUM ; ! "one"
эки NUM ; ! "two"
үч NUM ; ! ""
төрт NUM ; ! ""
беш NUM ; ! ""
алты NUM ; ! ""
жети NUM ; ! ""
сегиз NUM ; ! ""
тогуз NUM ; ! ""
он NUM ; ! ""
жыйырма NUM ; ! ""
отуз NUM ; ! ""
кырк NUM ; ! ""
элүү NUM ; ! ""
алтымыш NUM ; ! ""
жетимиш NUM ; ! ""
сексен NUM ; ! ""
токсон NUM ; ! ""
жүз NUM ; ! ""
миң NUM ; ! ""
миллион NUM ; ! ""
миллиард NUM ; ! ""

LEXICON NounStems

аалам N-INFL ; ! "universe"
аба N-INFL ; ! "air"
абал N-INFL ; 
адабият N-INFL ; ! "literature"
адам N-INFL ; ! "man, human, person"
адаш N-INFL ; 
адрес N-INFL ; 
азап N-INFL ; 
азат N-INFL ; 
азык N-INFL ; 
ай N-INFL ; ! "month"
айгыр N-INFL ; 
академия N-INFL ; 
акмак N-INFL ; 
акча N-INFL ; 
акыбал N-INFL ; 
акырет N-INFL ; 
акыркы N-INFL ; 
акырын N-INFL ; 
алакан N-INFL ; 
алкыш N-INFL ; 
алма N-INFL ; ! "apple"
апа N-INFL ; ! "mother"
арыз N-INFL ; 
арык N-INFL ; 
аскер N-INFL ; ! "soldier" 
аста N-INFL ; 
ат N-INFL ; 
ата N-INFL ; 
атак-даңк N-INFL ; 
аферист N-INFL ; 
аяк N-INFL ; 
аял N-INFL ; 
аят N-INFL ; 
аң N-INFL ; 
бадан N-INFL ; 
бажа N-INFL ; 
байрак N-INFL ; 
бакыт N-INFL ; 
бала N-INFL ; ! "child"
балдыз N-INFL ; 
балта N-INFL ; 
балтыркан N-INFL ; 
балчык N-INFL ; 
баш N-INFL ; ! "head"
башмак N-INFL ; 
башталгыч N-INFL ; 
баягы N-INFL ; 
баян N-INFL ; 
без N-INFL ; 
бейт N-INFL ; 
бекзаада N-INFL ; 
бел N-INFL ; 
белги N-INFL ; 
бет N-INFL ; 
бетер N-INFL ; 
бетпак N-INFL ; 
беш N-INFL ; 
беш N-INFL ; ! "" 
бешик N-INFL ; 
билдирүү N-INFL ; 
билек N-INFL ; 
билерик N-INFL ; 
билим N-INFL ; 
бирдик N-INFL ; 
бирөө N-INFL ; 
бирөөсү N-INFL ; 
бит N-INFL ; 
борбор N-INFL ; ! "capital"
бут N-INFL ; 
бысмылда N-INFL ; 
бычым N-INFL ; 
бээ N-INFL ; ! ""
бүлөө N-INFL ; 
бөбөк N-INFL ; 
бөз N-INFL ; 
гүл N-INFL ; 
дарак N-INFL ; 
дарек N-INFL ; 
дене N-INFL ; 
дос N-INFL ; ! "" 
дүйнө N-INFL ;  ! "world" (?)
дүнүйө N-INFL ; ! "world" (?)
жаз N-INFL ; ! "spring"
жай N-INFL ; ! "summer"
жан N-INFL ; ! "jan"
желек N-INFL ; 
жол N-INFL ; ! "way"
жыгач N-INFL ; 
жыл N-INFL ; ! "year"
жылдыз N-INFL ; ! "star"
жүз N-INFL ; 
зайып N-INFL ; 
заң N-INFL ; ! "law"
илим N-INFL ; 
индустрия N-INFL ; 
июль N-INFL ; ! "" 
каз N-INFL ; ! "" 
кап N-INFL ; ! "sack"
кат N-INFL ; ! "mail"
келесоо N-INFL ; 
кеме N-INFL ; ! "boat"
китеп N-INFL ; ! "book"
кол N-INFL ; ! "hand"
короо N-INFL ; ""
коом N-INFL ; ! "community"
кыз N-INFL ; ! "girl"
кыйноо N-INFL ; 
көз N-INFL ; ! "eye"
көрсөтүү N-INFL ; 
көрүү N-INFL ; ! "" 
майор N-INFL ; 
мал N-INFL ; ! ""
мамлекет N-INFL ; ! "state"
мектеп N-INFL ; ! "school"
мурун N-INFL ; ! "nose"
наристе N-INFL ; 
ини N-INFL ; ! "younger brother"
омийин N-INFL ; 
пул N-INFL ; 
саат N-INFL ; !"watch"
сабак N-INFL ; 
саз N-INFL ; 
сулуу N-INFL ; ! "" 
сыра N-INFL ; ! "beer"
тагдыр N-INFL ; 
таш N-INFL ; ! "" 
таң N-INFL ; ! "dawn"
токой N-INFL ; 
тоо N-INFL ; ! "" 
топ N-INFL ; ! "ball"
тор N-INFL ; ! "net"
туз N-INFL ; ! "salt"
төө N-INFL ; ! "" 
унаа N-INFL ; ! "" 
уу N-INFL ; 
ууч N-INFL ; 
үй N-INFL ; ! "house"
чака N-INFL ; ! "bucket"
чалап N-INFL ; 
челек N-INFL ; ! "bucket"
шекил N-INFL ; 
шерменде N-INFL ; 
шилекей N-INFL ; ! "" 
ылай N-INFL ; 
ыр N-INFL ; ! "song"
ысым N-INFL ; 
эне N-INFL ;
эркин N-INFL ; 
эл N-INFL ;  ! "nation"
үй N-INFL ; ! "house, home"
өзүмчүлдүк N-INFL ; ! "egoism, selfishness"
өлкө N-INFL ; ! "country, land"
өсүмдүк N-INFL ; ! "plant"
өтүнүч N-INFL ; 
өтүк N-INFL ; ! "shoe"
ручка N-INFL ; ! "pen"
банк N-INFL ; ! "bank"
секретар N-INFL ; ! "secretary (man)"
секретарша N-INFL ; ! "secretary (woman)"
антрополог N-INFL ; ! "anthropologist"
литература N-INFL ; ! "literature"
индустрия N-INFL ; ! "industry"
физиология N-INFL ; ! "physiology"
экология N-INFL ; ! "ecology"
география N-INFL ; ! "geography"
философия N-INFL ; ! "philosophy"
пиво N-INFL ; ! "industry"
редактор N-INFL ; ! "editor"
район N-INFL ; ! "rajon"
доктор N-INFL ; ! "doctor"
форма N-INFL ; ! "form"

азык% түлүк N-INFL ; 
бир% канча N-INFL ; 
жүзү% кара N-INFL ; 
кимдир% бирөө N-INFL ; 
кимдир% бирөөсү N-INFL ; 
кол% чабуу N-INFL ; 
миң% башы N-INFL ; 
сормо% саз N-INFL ; 
тиги% дүйнө N-INFL ; 
эси% жок N-INFL ; 
ак% сөөк N-INFL ; 

LEXICON PropernounStems

Абхазия NP-TOP ; ! ""
Австралия NP-TOP ; ! ""
Австрия NP-TOP ; ! ""
Азербайжан NP-TOP ; ! ""
Америка NP-TOP ; ! ""
Амстердам NP-TOP ; ! ""
Англия NP-TOP ; ! ""
Ангола NP-TOP ; ! ""
Анкара NP-TOP ; ! ""
Антарктида NP-TOP ; ! ""
Антарктика NP-TOP ; ! ""
Арабистан NP-TOP ; ! ""
Армения NP-TOP ; ! ""
Атланта NP-TOP ; ! ""
Афганистан NP-TOP ; ! ""
Африка NP-TOP ; ! ""
Багдад NP-TOP ; ! ""
Баку NP-TOP ; ! ""
Бангладеш NP-TOP ; ! ""
Барселона NP-TOP ; ! ""
Басра NP-TOP ; ! ""
Бейрут NP-TOP ; ! ""
Белгия NP-TOP ; ! ""
Белград NP-TOP ; ! ""
Берлин NP-TOP ; ! ""
Бишкек NP-TOP ; ! ""
Болгария NP-TOP ; ! ""
Боливия NP-TOP ; ! ""
Бразилия NP-TOP ; ! ""
Брюселъ NP-TOP ; ! ""
Будапешт NP-TOP ; ! ""
Бухарест NP-TOP ; ! ""
Габон NP-TOP ; ! ""
Гамбург NP-TOP ; ! ""
Гана NP-TOP ; ! ""
Гватемала NP-TOP ; ! ""
Голландия NP-TOP ; ! ""
Гондурас NP-TOP ; ! ""
Дания NP-TOP ; ! ""
Дубай NP-TOP ; ! ""
Дусселъдорф NP-TOP ; ! ""
Европа NP-TOP ; ! ""
Ереван NP-TOP ; ! ""
Жакарта NP-TOP ; ! ""
Израил NP-TOP ; ! ""
Индия NP-TOP ; ! ""
Индонезия NP-TOP ; ! ""
Ирак NP-TOP ; ! ""
Иран NP-TOP ; ! ""
Истанбул NP-TOP ; ! ""
Казакстан NP-TOP ; ! ""
Казакстан NP-TOP ; ! "Kazakhstan"
Камерун NP-TOP ; ! ""
Канада NP-TOP ; ! ""
Кеня NP-TOP ; ! ""
Керкүк NP-TOP ; ! ""
Кипр NP-TOP ; ! ""
Колън NP-TOP ; ! ""
Конго NP-TOP ; ! ""
Копенгаген NP-TOP ; ! ""
Куба NP-TOP ; ! ""
Кыргызстан NP-TOP ; ! ""
Кыргызстан NP-TOP ; ! "Kyrgyzstan"
Латвия NP-TOP ; ! ""
Либия NP-TOP ; ! ""
Лиссабон NP-TOP ; ! ""
Литва NP-TOP ; ! ""
Люксембург NP-TOP ; ! ""
Мадагаскар NP-TOP ; ! ""
Мадагаскар NP-TOP ; ! "Madagascar"
Мадрид NP-TOP ; ! ""
Малазия NP-TOP ; ! ""
Малъта NP-TOP ; ! ""
Манхеттен NP-TOP ; ! ""
Мезопотамия NP-TOP ; ! ""
Мексика NP-TOP ; ! ""
Татарстан NP-TOP ; ! "Tatarstan"
Финландия NP-TOP ; ! ""
Франкфурт NP-TOP ; ! ""
Франция NP-TOP ; ! ""
Хелсинки NP-TOP ; ! ""
Хорватия NP-TOP ; ! ""
Черногория NP-TOP ; ! ""
Чехословакия NP-TOP ; ! ""
Чеченистан NP-TOP ; ! ""
!Экватор NP-TOP ; ! ""
!Эрбил NP-TOP ; ! ""
!Эфиопия NP-TOP ; ! ""
!Эфиопия NP-TOP ; ! ""
Япония NP-TOP ; ! ""
Өзбекстан NP-TOP ; ! "Uzbekistan"

Бишкек NP-TOP ; ! "Bishkek"
Лондон NP-TOP ; ! ""

Ахмет NP-ANT-M ; ! "Axmet"
Мирлан NP-ANT-M ; ! "Mirlan"
Болот NP-ANT-M ; ! "Bolot"

!Элеонора NP-ANT-F ; ! "Eleanora"

LEXICON AdjecStems

кыргыз ADJ ; ! "Kyrgyz"
сулуу ADJ ; ! "beautiful"
жаман ADJ ; ! "bad"
сом ADJ ; ! "pure"
кыз ADJ ; ! ""
!эң ADJ ; ! "most"	

LEXICON AdvStems
 
эртең ADV ; ! "tomorrow"
эми ADV ; ! "now"
бат ADV ; ! "fast"
эң ADV ; ! "most"

LEXICON VerbStems

бар%<v%>%<exi%>:бар # ;
жок%<v%>%<neg%>%<exi%>:жок # ;

оку Vinfl ; ! "read"
укта Vinfl ; ! "sleep"
сүй Vinfl ; ! "love"
күл Vinfl ; ! "smile, laugh"
сүйлө Vinfl ; ! "speak"
ук Vinfl ; ! "listen"
айда Vinfl ; ! "drive"
түздө Vinfl ; ! ""
чий Vinfl ; ! "draw a line"
токто Vinfl ; ! "stop" 

ачу V-INFL ; ! "open"
бар V-INFL ; ! "go"
бил V-INFL ; ! "know"
бол V-INFL ; ! "be"
де V-INFL-SHORT ; ! "say"
жат V-INFL ; ! "come"
же V-INFL-SHORT ; ! "eat"
ич V-INFL ; ! "drink"
кел V-INFL ; ! "come"
кет V-INFL ; ! "go"
отур V-INFL ; ! "sit"
тур V-INFL ; ! "stand, live"

LEXICON Punctuation

.%<sent%>:. # ;
,%<cm%>:, # ;
