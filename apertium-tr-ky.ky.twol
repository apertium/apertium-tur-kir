Alphabet
А Б В Г Д Е Ё Ж З И И К Л М Н Ң О Ө П Р С Т У Ү Ф Х Ц Ч Ш Щ Ь Ы Ъ Э Ю Я
а б в г д е ё ж з и й к л м н ң о ө п р с т у ү ф х ц ч ш щ ь ы ъ э ю я
%{G%}:г %{D%}:д %{A%}:а %{I%}:ы %{N%}:н %{L%}:л %{B%}:б %>:0 ;

Sets

Cns = Б В Г Д Ж З Й К Л М Н Ң П Р С Т Ф Х Ц Ч Ш Щ Ь Ъ 
	  б в г д ж з й к л м н ң п р с т ф х ц ч ш щ ь ъ 
	        %{D%} %{G%} %{N%} %{L%} %{B%} ;
Vow =     А Е Ё И О Ө У Ү Ы Э Ю Я
            а е ё и о ө у ү ы э ю я 
            %{A%} %{I%} ;
FrontVow = И Ө Е Ү Э и ө ү э е;
BackVow = А О У Ы а о у ы;
FrontUnrndVow = И Е Э и э е;
BackUnrndVow = А Ы а ы;
FrontRndVow = Ө Ү ө ү ;
BackRndVow =  О У о у ;
BackLowRndVow = О о ;

SonCns = й р Й Р ;
UnvoicedNonSonCns = К П С Т Ф Х Ц Ч Ш Щ  
	                к п с т ф х ц ч ш щ ;

VoicedNonSonCns = Б В Г Д Ж З Л М Н Ң 
                  б в г д ж з л м н ң ;

CnsOrFrontUnrnd = Cns FrontUnrndVow %> ;
CnsOrFrontRnd = Cns FrontRndVow %> ;
CnsOrBackRnd = Cns BackRndVow %> ;

CnsOrFrontUnrndV = Cns FrontUnrndVow ;
CnsOrFrontRndV = Cns FrontRndVow ;
CnsOrBackRndV = Cns BackRndVow ;
CnsOrBackLowRndVow = Cns BackLowRndVow ;
	   
NonBack = Cns FrontVow %> ;
NonFront = Cns BackVow %> ;
 
Rules

"Katkalan harmony in consonants 2"
%{G%}:к <=> UnvoicedNonSonCns %>: _ ;

! Trond

"Suffix Vow deletion in V-stems"
%{I%}:0 <=> Vow %>: _ ;

"Front unrnd harmony in suffixes"
Vx:Vy <=> FrontUnrndVow: CnsOrFrontUnrnd:* Cns %>: CnsOrFrontUnrndV:* _ ; ! C>*V*, C>C*V*
          FrontUnrndVow: CnsOrFrontUnrnd:* %>: Cns CnsOrFrontUnrndV:* _ ; ! V>C*V*
	where Vx in ( %{A%} %{I%} )
	      Vy in (   е     и   )
	      matched ;

"Front Rnd harmony in suffixes"
Vx:Vy <=> FrontRndVow: CnsOrFrontRnd:* Cns %>: CnsOrFrontRndV:* _ ; ! C>*V*, C>C*V*
          FrontRndVow: CnsOrFrontRnd:* %>: Cns CnsOrFrontRndV:* _ ; ! V>C*V*
	where Vx in ( %{A%} %{I%} )
	      Vy in (   ө     ү   )
	      matched ;

"Back Low Rnd harmony in suffixes"
 %{A%}:о <=> BackLowRndVow: CnsOrBackLowRnd:* Cns %>:     CnsOrBackLowRndV:* _ ; ! C>*V*, C>C*V*
             BackLowRndVow: CnsOrBackLowRnd:*     %>: Cns CnsOrBackLowRndV:* _ ; ! V>C*V*


"Back High Rnd harmony in suffixes"
%{I%}:у <=> BackRndVow: CnsOrBackRnd:* Cns %>: CnsOrBackRndV:* _ ; ! C>*V*, C>C*V*
            BackRndVow: CnsOrBackRnd:* %>: Cns CnsOrBackRndV:* _ ; ! V>C*V*

"L Desonorisation"
%{L%}:д <=> VoicedNonSonCns %>:  _ ;

"N Desonorisation"
%{N%}:д <=> [ SonCns | VoicedNonSonCns ] %>:  _ ;

"L Desonorisation and Devoicing"
Cx:т <=> UnvoicedNonSonCns %>: _ ;
	where Cx in ( %{L%} %{N%} ) ;


! ndt	N
! ldt	L
! gk	G
! dt	D
! mbp	M

! The error in our analys is that VH is treated as symmetric wrt. triggers.
! It is not, there is a front/back distinction, more vowels trigger а:
! root -≥ suffix:
! а,ы,у -≥ а
! о -≥ о
! е,и -≥ е
! ү,ө -≥ ө

