Alphabet
А Б В Г Д Е Ё Ж З И И К Л М Н Ң О Ө П Р С Т У Ү Ф Х Ц Ч Ш Щ Ь Ы Ъ Э Ю Я
а б в г д е ё ж з и й к л м н ң о ө п р с т у ү ф х ц ч ш щ ь ы ъ э ю я
%{G%}:г %{D%}:д %{N%}:н %{L%}:л %{B%}:б %{E%}:а %{P%}:п 
%{A%}:а %{I%}:ы %{U%}:у 
%>:0 ; 

Sets

Cns = Б В Г Д Ж З Й К Л М Н Ң П Р С Т Ф Х Ц Ч Ш Щ Ь Ъ 
	  б в г д ж з й к л м н ң п р с т ф х ц ч ш щ ь ъ 
	  %{D%} %{G%} %{N%} %{L%} %{B%} %{P%} ;
Vow =     А Е Ё И О Ө У Ү Ы Э Ю Я
          а е ё и о ө у ү ы э ю я 
          %{A%} %{I%} %{U%} %{E%};
FrontVow = И Ө Е Ү Э и ө ү э е ;
BackVow = А О У Ы а о у ы ;
FrontUnrndVow = И Е Э и э е ; 
BackUnrndVow = А Ы а ы ;
FrontRndVow = Ө Ү ө ү ;
BackRndVow =  О У о у ;
BackLowRndVow = О о ;
UyanCons = м ң М Ң ;

SonCns = й р Й Р ;
UnvoicedNonSonCns = К П С Т Ф Х Ц Ч Ш Щ  
	                к п с т ф х ц ч ш щ ;

VoicedNonSonCns = Б В Г Д Ж З Л М Н Ң Й Р 
                  б в г д ж з л м н ң й р ;

VoicedNonSonCnsNoRNoIj = Б В Г Д Ж З Л М Н Ң  
                  б в г д ж з л м н ң ;

! These three sets include the suffix border, and accounts for long-distance VH
CnsFrontUnrndVowSB = Cns FrontUnrndVow %> ;
CnsFrontRndVowSB = Cns FrontRndVow %> ;
CnsBackRndVowSB = Cns BackRndVow %> ;

! These sets do not continue suffix borders.
CnsFrontUnrndVow = Cns FrontUnrndVow %{I%} %{A%} %{U%} %{E%};
CnsFrontRndVow = Cns FrontRndVow  %{I%} %{A%} %{U%} %{E%};
CnsBackRndVow = Cns BackRndVow  %{I%} %{A%} %{U%} %{E%} ;
CnsBackLowRndVow = Cns BackLowRndVow  %{I%} %{A%} %{U%} %{E%};
	   
 
Rules

! этиш<n><pl><gen>:этиш>{L}{A}р>{N}{I}н

"Suffix Vow deletion in V-stems"
%{I%}:0 <=> Vow %>: _ ;


"Front Unrnd harmony in suffixes"
Vx:Vy <=> :FrontUnrndVow CnsFrontUnrndVowSB:* Cns %>:     CnsFrontUnrndVow:* _ ; !  ?C>?_
          :FrontUnrndVow CnsFrontUnrndVowSB:*     %>: Cns CnsFrontUnrndVow:* _ ; !   ?>C_
	where Vx in ( %{A%} %{I%} %{U%} %{E%} )
	      Vy in (   е     и     ү     е   )
	      matched ;

"Front Rnd harmony in suffixes"
Vx:Vy <=> :FrontRndVow CnsFrontRndVowSB:* Cns %>:     CnsFrontRndVow:* _ ; ! ?C>?_
          :FrontRndVow CnsFrontRndVowSB:*     %>: Cns CnsFrontRndVow:* _ ; !  ?>C_
	where Vx in ( %{A%} %{I%} %{U%} %{E%} )
	      Vy in (   ө     ү     ү     ө   )
	      matched ;

"Back Low Rnd harmony in suffixes"
Vx:Vy <=> :BackLowRndVow CnsBackLowRndVow:* Cns %>:     CnsBackLowRndVow:* _ ; ! ?C>?_
          :BackLowRndVow CnsBackLowRndVow:*     %>: Cns CnsBackLowRndVow:* _ ; !  ?>C_
        where Vx in ( %{A%} %{I%} %{U%} %{E%} ) 
              Vy in (   о     у     у     о   )
              matched ; 

"Back High Rnd harmony in suffixes"
Vx:Vy <=> :BackRndVow CnsBackRndVowSB:* Cns %>:     CnsBackRndVow:* _ ; ! ?C>?_
            :BackRndVow CnsBackRndVowSB:*     %>: Cns CnsBackRndVow:* _ ; !  ?>C_
        where Vx in (       %{I%} %{U%} %{E%} )
              Vy in (         у     у     о   )
              matched ; 

"L Desonorisation"
%{L%}:д <=> VoicedNonSonCnsNoRNoIj %>:  _ ;

"N Desonorisation"
%{N%}:д <=> [ SonCns | VoicedNonSonCns ] %>:  _ ;

"L Desonorisation and devoicing"
Cx:т <=> [ UnvoicedNonSonCns | UnvoicedNonSonCns ь ] %>: _ ;
	where Cx in ( %{L%} %{N%} ) ;

"Katkalan harmony in consonants 2"
%{G%}:к <=> UnvoicedNonSonCns %>: _ ;

"Katkalan harmony in consonants 1"
%{D%}:т <=> UnvoicedNonSonCns %>: _ ;

"Мягкий знак deletion before suffix"
ь:0 <=> _ %>: ;

"Realisation of converb marker after vowels"
%{E%}:й <=> Vow %>: _ ;

"'с' deletion in possessive after consonant"
с:0 <=> Cns %>: _ %{I%}: ; 

"'P' desonorisation before >c{I}"
п:б <=> Vow _ %>: с: %{I%}: ;

"'г' deletion after uyan consonants"
%{G%}:0 <=> UyanCons: %>: _ %{A%}: ;

"'г' desonorisation after I"
%{G%}:н <=> %{I%}: %>: _ %{A%}: ;

"'н' addition between {I} and {D} in locative" 
0:н <=> %{I%}: %>: _ %{D%}: %{A%}: [ \н | .#. ] ;

"{D} -> н after {I} and before {A} in ablative" 
%{D%}:н <=> %{I%}: %>: _ %{A%}: н ;

"P voicing"
п:б <=> Vow _ %>: Vow ;

"K voicing"
к:г <=> Vow _ %>: Vow ;

"B -> п in negative"
%{B%}:п <=> UnvoicedNonSonCns: %>: _  ;


! ndt	N
! ldt	L
! gk	G
! dt	D
! mbp	M

