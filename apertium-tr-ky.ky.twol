Alphabet
А Б В Г Д Е Ё Ж З И И К Л М Н Ң О Ө П Р С Т У Ү Ф Х Ц Ч Ш Щ Ь Ы Ъ Э Ю Я
а б в г д е ё ж з и й к л м н ң о ө п р с т у ү ф х ц ч ш щ ь ы ъ э ю я
%{G%}:г %{D%}:д %{A%}:а %{I%}:ы %{N%}:н %{L%}:л %{B%}:б %>:0 ;

Sets

Consonant = Б В Г Д Ж З Й К Л М Н Ң П Р С Т Ф Х Ц Ч Ш Щ Ь Ъ 
	        б в г д ж з й к л м н ң п р с т ф х ц ч ш щ ь ъ 
	        %{D%} %{G%} %{N%} %{L%} %{B%} ;
Vowel =     А Е Ё И О Ө У Ү Ы Э Ю Я
            а е ё и о ө у ү ы э ю я 
            %{A%} %{I%} ;
FrontVowel = И Ө Е Ү Э и ө ү э е;
BackVowel = А О У Ы а о у ы;
FrontUnroundedVowel = И Е Э и э е;
BackUnroundedVowel = А Ы а ы;
FrontRoundVowel = Ө Ү ө ү ;
BackRoundVowel =  О У о у ;
FrontConsonant = К П С Т Ф Х Ц Ч Ш Щ к п с т ф х ц ч ш щ;
BackConsonant =  Б В Г Д Ж З Й Л М Н Ң Р б в г д ж з й л м н ң р;

SonorantConsonant = й р Й Р ;
UnvoicedNonSonorantConsonant = К П С Т Ф Х Ц Ч Ш Щ  
	                           к п с т ф х ц ч ш щ ;

VoicedNonSonorantConsonant = Б В Г Д Ж З Л М Н Ң 
                             б в г д ж з л м н ң ;

ConsOrFrontUnrounded = Consonant FrontUnroundedVowel %> ;
ConsOrFrontRounded = Consonant FrontRoundVowel %> ;
ConsOrBackRounded = Consonant BackRoundVowel %> ;

ConsOrFrontUnroundedV = Consonant FrontUnroundedVowel ;
ConsOrFrontRoundedV = Consonant FrontRoundVowel ;
ConsOrBackRoundedV = Consonant BackRoundVowel ;
	   
NonBack = Consonant FrontVowel %> ;
NonFront = Consonant BackVowel %> ;
NonBackC = FrontVowel Vowel FrontConsonant %>;
NonFrontC = BackVowel Vowel BackConsonant %>;

 
Rules

"Katkalan harmony in consonants 2"
%{G%}:к <=> FrontConsonant: NonBackC:* %>: NonBackC:* _ ;

! Trond

"Suffix Vowel deletion in V-stems"
%{I%}:0 <=> Vowel %>: _ ;

"Front unrounded harmony in suffixes"
Vx:Vy <=> FrontUnroundedVowel: ConsOrFrontUnrounded:* Consonant %>: ConsOrFrontUnroundedV:* _ ; ! C>*V*, C>C*V*
          FrontUnroundedVowel: ConsOrFrontUnrounded:* %>: Consonant ConsOrFrontUnroundedV:* _ ; ! V>C*V*
	where Vx in ( %{A%} %{I%} )
	      Vy in (   е     и   )
	      matched ;

"Front Round harmony in suffixes"
Vx:Vy <=> FrontRoundVowel: ConsOrFrontRounded:* Consonant %>: ConsOrFrontRoundedV:* _ ; ! C>*V*, C>C*V*
          FrontRoundVowel: ConsOrFrontRounded:* %>: Consonant ConsOrFrontRoundedV:* _ ; ! V>C*V*
	where Vx in ( %{A%} %{I%} )
	      Vy in (   ө     ү   )
	      matched ;

"Back Round harmony in suffixes"
Vx:Vy <=> BackRoundVowel: ConsOrBackRounded:* Consonant %>: ConsOrBackRoundedV:* _ ; ! C>*V*, C>C*V*
          BackRoundVowel: ConsOrBackRounded:* %>: Consonant ConsOrBackRoundedV:* _ ; ! V>C*V*
	where Vx in ( %{A%} %{I%} )
	      Vy in (   о     у   )
	      matched ;

"L Desonorisation"
%{L%}:д <=> VoicedNonSonorantConsonant %>:  _ ;

"N Desonorisation"
%{N%}:д <=> [ SonorantConsonant | VoicedNonSonorantConsonant ] %>:  _ ;

"L Desonorisation and Devoicing"
Cx:т <=> UnvoicedNonSonorantConsonant %>: _ ;
	where Cx in ( %{L%} %{N%} ) ;


! ndt	N
! ldt	L
! gk	G
! dt	D
! mbp	M
